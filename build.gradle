plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.openapi.generator' version '7.10.0'
}

group = 'uk.co.payr'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(23)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'com.google.flogger:flogger:0.8'
	implementation 'com.google.flogger:flogger-system-backend:0.8'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'com.google.code.gson:gson:2.11.0'
	implementation 'com.squareup.okhttp3:okhttp:4.12.0'
	implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation 'io.gsonfire:gson-fire:1.9.0'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

openApiGenerate {
	generatorName = 'java'
	inputSpec = "$rootDir/src/main/resources/spec/user-api.yaml" // Path to your OpenAPI spec
	outputDir = "$buildDir/generated" // Output directory for generated code
	apiPackage = 'uk.co.payr.payremailapi.api'
	modelPackage = 'uk.co.payr.payremailapi.model'
	invokerPackage = 'uk.co.payr.payremailapi.invoker'
	validateSpec = false
	skipValidateSpec = true
	configOptions = [
			dateLibrary: 'threetenbp',
			useJakartaEe: 'true'
	]
}

sourceSets {
	main {
		java {
			srcDir "$buildDir/generated/src/main/java"
		}
	}
}

compileJava.dependsOn tasks.openApiGenerate
